#version 460

#define GROUP_SIZE         8
#define GROUP_THREAD_COUNT (GROUP_SIZE * GROUP_SIZE)

layout(local_size_x = GROUP_SIZE, local_size_y = GROUP_SIZE) in;

layout(binding = 0) uniform sampler2D inputTex;
layout(rgba16f, binding = 0) uniform writeonly image2D outputImg;

float W_f(float x, float e0, float e1) {
	float a = clamp((x - e0) / (e1 - e0), 0, 1);
	return a * a * (3.0 - 2.0 * a);
}

float H_f(float x, float e0, float e1) {
	return clamp((x - e0) / (e1 - e0), 0, 1);
}

float GranTurismoTonemapper(float x) {
	const float P = 1.0, a = 1.0, m = 0.22, l = 0.4, c = 1.33, b = 0.0;
	const float l0 = ((P - m) * l) / a;
	const float S0 = m + l0;
	const float S1 = m + a * l0;
	const float C2 = (a * P) / (P - S1);
	const float w0_x = 1.0 - W_f(x, 0.0, m);
	const float w2_x = H_f(x, S0, S1);
	const float w1_x = 1.0 - w0_x - w2_x;
	const float T_x = m * pow(abs(x / m), c) + b;
	const float L_x = m + a * (x - m);
	const float S_x = P - (P - S1) * exp(-(C2 * (x - S0)) / P);
	return T_x * w0_x + L_x * w1_x + S_x * w2_x;
}

void main() {
	const ivec2 pixelCoord = ivec2(gl_GlobalInvocationID);

	vec2 inputSize = textureSize(inputTex, 0);

	if (pixelCoord.x >= inputSize.x || pixelCoord.y >= inputSize.y) {
		return;
	}

	vec2 uv = (pixelCoord + 0.5) / inputSize;

	vec4 col = texture(inputTex, uv);

	col.r = GranTurismoTonemapper(col.r);
	col.g = GranTurismoTonemapper(col.g);
	col.b = GranTurismoTonemapper(col.b);

	imageStore(outputImg, pixelCoord, col);
}
