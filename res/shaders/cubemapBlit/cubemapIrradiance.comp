#version 460

#define GROUP_SIZE 8
#define GROUP_THREAD_COUNT (GROUP_SIZE * GROUP_SIZE)

layout(local_size_x = GROUP_SIZE, local_size_y = GROUP_SIZE, local_size_z = 6) in;

uniform samplerCube environmentMap;

layout(rgba16f, binding = 0) uniform writeonly imageCube outputImg;

const float PI = 3.14159265359;

const vec3 directions[] = {
	vec3( 1,  0,  0),
	vec3(-1,  0,  0),
	vec3( 0,  1,  0),
	vec3( 0, -1,  0),
	vec3( 0,  0,  1),
	vec3( 0,  0, -1),
};

const vec3 ups[] = {
	vec3( 0,  -1,  0),
	vec3( 0,  -1,  0),
	vec3( 0,  0,  1),
	vec3( 0,  0, -1),
	vec3( 0, -1,  0),
	vec3( 0, -1,  0),
};

const vec3 rights[] = {
	vec3( 0,  0, -1),
	vec3( 0,  0,  1),
	vec3( 1,  0,  0),
	vec3( 1,  0,  0),
	vec3( 1,  0,  0),
	vec3(-1,  0,  0),
};

void main() {
	const ivec3 pixelCoord = ivec3(gl_GlobalInvocationID);

	const vec2 outputSize = imageSize(outputImg);

	if (pixelCoord.x >= outputSize.x || pixelCoord.y >= outputSize.y) {
		return;
	}

	const vec2 localUV = (pixelCoord.xy / outputSize) - 0.5;

	const vec3 local_forward = directions[gl_GlobalInvocationID.z];
	const vec3 local_up = ups[gl_GlobalInvocationID.z];
	const vec3 local_right = rights[gl_GlobalInvocationID.z];

	const vec3 localPos = 0.5 * local_forward + local_right * localUV.x + local_up * localUV.y;

	const vec3 normal = normalize(localPos);

	vec3 irradiance = vec3(0.0);  

	vec3 up  = vec3(0.0, 1.0, 0.0);
	const vec3 right = normalize(cross(up, normal));
	up = normalize(cross(normal, right));

	const float sampleDelta = 0.01;
	float nrSamples = 0.0; 

	for (float phi = 0.0; phi < 2.0 * PI; phi += sampleDelta) {
		for (float theta = 0.0; theta < 0.5 * PI; theta += sampleDelta) {
			// spherical to cartesian (in tangent space)
			const vec3 tangentSample = vec3(sin(theta) * cos(phi),  sin(theta) * sin(phi), cos(theta));
			// tangent space to world
			const vec3 sampleVec = tangentSample.x * right + tangentSample.y * up + tangentSample.z * normal; 
			
			vec3 addColor = texture(environmentMap, sampleVec).rgb; // The environment map sometimes samples to NaN, we need to guard against it
			
			irradiance += addColor * cos(theta) * sin(theta);
			nrSamples++;
		}
	}
	irradiance = PI * irradiance * (1.0 / float(nrSamples));

	imageStore(outputImg, pixelCoord, vec4(irradiance, 0));
}